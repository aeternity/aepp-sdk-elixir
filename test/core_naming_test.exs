defmodule CoreNamingTest do
  use ExUnit.Case
  alias AeppSDK.Utils.{Keys, Serialization}
  alias AeppSDK.{AENS, Account}

  setup_all do
    Code.require_file("test_utils.ex", "test/")
    TestUtils.get_test_data()
  end

  @tag :travis_test
  test "naming workflow", setup do
    # Pre-claim a name
    pre_claim = AENS.preclaim(setup.client, "test.test")
    assert match?({:ok, _}, pre_claim)

    # Claim a name
    {:ok, pre_claim_info} = pre_claim
    claim = AENS.claim(setup.client, "test.test", pre_claim_info.name_salt)
    assert match?({:ok, _}, claim)

    # Update a name
    list_of_pointers = [
      {AeppSDK.Utils.Keys.public_key_to_binary(setup.valid_pub_key),
       Serialization.id_to_record(
         Keys.public_key_to_binary(setup.valid_pub_key),
         :account
       )}
    ]

    update =
      AENS.update_name(
        setup.client,
        "test.test",
        49_999,
        list_of_pointers,
        50_000
      )

    assert match?({:ok, _}, update)

    # Spending to another account, in order to transfer a name to it
    spend =
      Account.spend(
        %{setup.client | gas_price: 1_000_000_000_000},
        setup.valid_pub_key,
        setup.amount
      )

    assert match?({:ok, _}, spend)

    # Transfer a name to another account
    transfer = AENS.transfer_name(setup.client, "test.test", setup.valid_pub_key)

    assert match?({:ok, _}, transfer)

    # Pre-claim a new name (name_salt = 888)
    pre_claim = AENS.preclaim(setup.client, "newtest.test", 888)
    assert match?({:ok, _}, pre_claim)

    # Claim a new name
    claim = AENS.claim(setup.client, "newtest.test", 888)
    assert match?({:ok, _}, claim)

    # Revoke a new name
    revoke = AENS.revoke_name(setup.client, "newtest.test")
    assert match?({:ok, _}, revoke)
  end

  @tag :travis_test
  test "test naming workflow using pipe operator ", setup do
    # Pre-claim with claim (autogenerated salt)
    {:ok, claim_result} =
      setup.client
      |> AENS.preclaim("name.test")
      |> AENS.claim()

    # Update
    update_result =
      {:ok, claim_result}
      |> AENS.update()

    assert match?({:ok, _}, update_result)

    # Transfer
    transfer_result =
      {:ok, claim_result}
      |> AENS.transfer(setup.valid_pub_key)

    assert match?({:ok, _}, transfer_result)

    # Revoke
    # New preclaim, claim and revoke
    {:ok, new_revoke_result} =
      setup.client
      |> AENS.preclaim("newname.test")
      |> AENS.claim()
      |> AENS.revoke()

    assert match?(
             {:ok, %AeternityNode.Model.Error{reason: "Name revoked"}},
             AeternityNode.Api.NameService.get_name_entry_by_name(
               setup.client.connection,
               new_revoke_result.name
             )
           )
  end

  @tag :travis_test
  test "test naming error handling", setup do
    # Claim a new name
    assert {:ok, _claim_result} =
             setup.client
             |> AENS.preclaim("name123.test")
             |> AENS.claim()

    # Try to claim again already claimed name
    assert {:error, _claim_result} =
             setup.client
             |> AENS.preclaim("name123.test")
             |> AENS.claim()
  end
end
